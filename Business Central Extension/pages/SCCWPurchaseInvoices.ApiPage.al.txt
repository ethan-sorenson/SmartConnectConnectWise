page 64061 "SSCW - Purchase Invoices"
{
    APIVersion = 'v2.0';
    APIPublisher = 'eOneSolutions';
    APIGroup = 'ConnectWise';
    EntityCaption = 'Purchase Invoice';
    EntitySetCaption = 'Purchase Invoices';
    ChangeTrackingAllowed = true;
    DelayedInsert = true;
    EntityName = 'purchaseInvoice';
    EntitySetName = 'purchaseInvoices';
    ODataKeyFields = Id;
    PageType = API;
    SourceTable = "Purch. Inv. Entity Aggregate";
    Extensible = true;

    layout
    {
        area(content)
        {
            repeater(Group)
            {
                field(id; rec.Id)
                {
                    Editable = false;
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo(Id));
                    end;
                }
                field(number; rec."No.")
                {
                    Editable = false;
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("No."));
                    end;
                }
                field(invoiceDate; rec."Document Date")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Document Date"));
                        WORKDATE(rec."Document Date");
                    end;
                }
                field(postingDate; rec."Posting Date")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Posting Date"));
                    end;
                }
                field(dueDate; rec."Due Date")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Due Date"));
                    end;
                }
                field(vendorInvoiceNumber; rec."Vendor Invoice No.")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Vendor Invoice No."));
                    end;
                }
                field(vendorNumber; rec."Buy-from Vendor No.")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from Vendor No."));
                    end;
                }
                field(vendorName; rec."Buy-from Vendor Name") { Editable = false; }
                field(payToName; rec."Pay-to Name") { Editable = false; }
                field(payToContact; rec."Pay-to Contact")
                {
                    Editable = false;
                    trigger OnValidate()
                    begin
                        if xRec."Pay-to Contact" <> rec."Pay-to Contact" then
                            RegisterFieldSet(rec.FieldNo("Pay-to Contact"));
                    end;
                }
                field(payToVendorNumber; rec."Pay-to Vendor No.")
                {
                    trigger OnValidate()
                    begin
                        if PayToVendor."No." <> '' then
                            exit;

                        if not PayToVendor.Get(rec."Pay-to Vendor No.") then
                            Error(CouldNotFindPayToVendorErr);

                        rec."Pay-to Vendor Id" := PayToVendor.SystemId;
                        RegisterFieldSet(rec.FieldNo("Pay-to Vendor Id"));
                        RegisterFieldSet(rec.FieldNo("Pay-to Vendor No."));
                    end;
                }
                field(shipToName; rec."Ship-to Name")
                {
                    trigger OnValidate()
                    begin
                        if xRec."Ship-to Name" <> rec."Ship-to Name" then begin
                            rec."Ship-to Code" := '';
                            RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                            RegisterFieldSet(rec.FieldNo("Ship-to Name"));
                        end;
                    end;
                }
                field(shipToContact; rec."Ship-to Contact")
                {
                    trigger OnValidate()
                    begin
                        if xRec."Ship-to Contact" <> rec."Ship-to Contact" then begin
                            rec."Ship-to Code" := '';
                            RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                            RegisterFieldSet(rec.FieldNo("Ship-to Contact"));
                        end;
                    end;
                }
                field(buyFromAddressLine1; rec."Buy-from Address")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from Address"));
                    end;
                }
                field(buyFromAddressLine2; rec."Buy-from Address 2")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from Address 2"));
                    end;
                }
                field(buyFromCity; rec."Buy-from City")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from City"));
                    end;
                }
                field(buyFromCountry; rec."Buy-from Country/Region Code")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from Country/Region Code"));
                    end;
                }
                field(buyFromCounty; rec."Buy-from County")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from County"));
                    end;
                }
                field(buyFromPostCode; rec."Buy-from Post Code")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Buy-from Post Code"));
                    end;
                }
                field(shipToAddressLine1; rec."Ship-to Address")
                {
                    trigger OnValidate()
                    begin
                        rec."Ship-to Code" := '';
                        RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                        RegisterFieldSet(rec.FieldNo("Ship-to Address"));
                    end;
                }
                field(shipToAddressLine2; rec."Ship-to Address 2")
                {
                    trigger OnValidate()
                    begin
                        rec."Ship-to Code" := '';
                        RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                        RegisterFieldSet(rec.FieldNo("Ship-to Address 2"));
                    end;
                }
                field(shipToCity; rec."Ship-to City")
                {
                    trigger OnValidate()
                    begin
                        rec."Ship-to Code" := '';
                        RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                        RegisterFieldSet(rec.FieldNo("Ship-to City"));
                    end;
                }
                field(shipToCountry; rec."Ship-to Country/Region Code")
                {
                    trigger OnValidate()
                    begin
                        rec."Ship-to Code" := '';
                        RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                        RegisterFieldSet(rec.FieldNo("Ship-to Country/Region Code"));
                    end;
                }
                field(shipToState; rec."Ship-to County")
                {
                    trigger OnValidate()
                    begin
                        rec."Ship-to Code" := '';
                        RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                        RegisterFieldSet(rec.FieldNo("Ship-to County"));
                    end;
                }
                field(shipToPostCode; rec."Ship-to Post Code")
                {
                    trigger OnValidate()
                    begin
                        rec."Ship-to Code" := '';
                        RegisterFieldSet(rec.FieldNo("Ship-to Code"));
                        RegisterFieldSet(rec.FieldNo("Ship-to Post Code"));
                    end;
                }
                field(payToAddressLine1; rec."Pay-to Address") { Editable = false; }
                field(payToAddressLine2; rec."Pay-to Address 2")
                {
                    Editable = false;
                }
                field(payToCity; rec."Pay-to City") { Editable = false; }
                field(payToCountry; rec."Pay-to Country/Region Code") { Editable = false; }
                field(payToState; rec."Pay-to County") { Editable = false; }
                field(payToPostCode; rec."Pay-to Post Code") { Editable = false; }
                field(shortcutDimension1Code; rec."Shortcut Dimension 1 Code")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Shortcut Dimension 1 Code"));
                    end;
                }
                field(shortcutDimension2Code; rec."Shortcut Dimension 2 Code")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Shortcut Dimension 2 Code"));
                    end;
                }
                field(currencyCode; CurrencyCodeTxt)
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Currency Code"));
                    end;
                }
                field(orderNumber; rec."Order No.") { Editable = false; }
                field(pricesIncludeTax; rec."Prices Including VAT")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Prices Including VAT"));
                    end;
                }
                part(_Dimension_Set_Lines; "APIV2 - Dimension Set Lines")
                {
                    Caption = 'Dimension Set Lines';
                    EntityName = 'dimensionSetLine';
                    EntitySetName = 'dimensionSetLines';
                    SubPageLink = "Parent Id" = Field(Id), "Parent Type" = const("Purchase Invoice");
                }
                part(_GL_Lines; "SSCW - Purchase Invoice Lines")
                {
                    Caption = 'Lines';
                    EntityName = 'purchaseInvoiceLine';
                    EntitySetName = 'purchaseInvoiceLines';
                    SubPageLink = "Document No." = Field("No."), "Document Type" = const("Invoice");
                }
                part(_Item_Lines; "SSCW - Purchase Invoice Lines")
                {
                    Caption = 'Lines';
                    EntityName = 'purchaseInvoiceLine';
                    EntitySetName = 'purchaseInvoiceLines';
                    SubPageLink = "Document No." = Field("No."), "Document Type" = const("Invoice");
                }
                field(discountAmount; rec."Invoice Discount Amount")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Invoice Discount Amount"));
                        InvoiceDiscountAmount := rec."Invoice Discount Amount";
                        DiscountAmountSet := true;
                    end;
                }
                field(discountAppliedBeforeTax; rec."Discount Applied Before Tax") { }
                field(totalAmountExcludingTax; rec.Amount) { Editable = false; }
                field(totalTaxAmount; rec."Total Tax Amount")
                {
                    Editable = false;
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Total Tax Amount"));
                    end;
                }
                field(totalAmountIncludingTax; rec."Amount Including VAT")
                {
                    trigger OnValidate()
                    begin
                        RegisterFieldSet(rec.FieldNo("Amount Including VAT"));
                    end;
                }
                field(status; rec.Status) { Editable = false; }
                field(lastModifiedDateTime; rec.SystemModifiedAt) { }
            }
        }
    }

    actions
    {
    }

    trigger OnAfterGetRecord()
    var
        PurchInvAggregator: Codeunit "Purch. Inv. Aggregator";
    begin
        SetCalculatedFields();
        if HasWritePermission then
            PurchInvAggregator.RedistributeInvoiceDiscounts(Rec);
    end;

    trigger OnDeleteRecord(): Boolean
    var
        PurchInvAggregator: Codeunit "Purch. Inv. Aggregator";
    begin
        PurchInvAggregator.PropagateOnDelete(Rec);

        exit(false);
    end;

    trigger OnInsertRecord(BelowxRec: Boolean): Boolean
    var
        PurchInvAggregator: Codeunit "Purch. Inv. Aggregator";
    begin
        CheckBuyFromVendor();

        PurchInvAggregator.PropagateOnInsert(Rec, TempFieldBuffer);
        UpdateDiscount();

        SetCalculatedFields();

        PurchInvAggregator.RedistributeInvoiceDiscounts(Rec);

        exit(false);
    end;

    trigger OnModifyRecord(): Boolean
    var
        PurchInvAggregator: Codeunit "Purch. Inv. Aggregator";
    begin
        if xRec.Id <> rec.Id then
            Error(CannotChangeIDErr);

        PurchInvAggregator.PropagateOnModify(Rec, TempFieldBuffer);
        UpdateDiscount();

        SetCalculatedFields();

        PurchInvAggregator.RedistributeInvoiceDiscounts(Rec);

        exit(false);
    end;

    trigger OnNewRecord(BelowxRec: Boolean)
    begin
        ClearCalculatedFields();
    end;

    trigger OnOpenPage()
    begin
        CheckPermissions();
    end;

    var
        TempFieldBuffer: Record "Field Buffer" temporary;
        PayToVendor: Record "Vendor";
        GraphMgtGeneralTools: Codeunit "Graph Mgt - General Tools";
        LCYCurrencyCode: Code[10];
        CurrencyCodeTxt: Text;
        CannotChangeIDErr: Label 'The "id" cannot be changed.', Comment = 'id is a field name and should not be translated.';
        BuyFromVendorNotProvidedErr: Label 'A "vendorNumber" or a "vendorID" must be provided.', Comment = 'vendorNumber and vendorID are field names and should not be translated.';
        CouldNotFindPayToVendorErr: Label 'The pay-to vendor cannot be found.';
        BlankGUID: Guid;
        DraftInvoiceActionErr: Label 'The action can be applied to a draft invoice only.';
        CannotFindInvoiceErr: Label 'The invoice cannot be found.';
        DiscountAmountSet: Boolean;
        InvoiceDiscountAmount: Decimal;
        HasWritePermission: Boolean;
        PurchaseInvoicePermissionsErr: Label 'You do not have permissions to read Purchase Invoices.';

    local procedure SetCalculatedFields()
    begin
        Rec.LoadFields("Currency Code");
        CurrencyCodeTxt := GraphMgtGeneralTools.TranslateNAVCurrencyCodeToCurrencyCode(LCYCurrencyCode, rec."Currency Code");
    end;

    local procedure UpdateDiscount()
    var
        PurchaseHeader: Record "Purchase Header";
        PurchInvAggregator: Codeunit "Purch. Inv. Aggregator";
        PurchCalcDiscByType: Codeunit "Purch - Calc Disc. By Type";
    begin
        if rec.Posted then
            exit;

        if not DiscountAmountSet then begin
            PurchInvAggregator.RedistributeInvoiceDiscounts(Rec);
            exit;
        end;

        PurchaseHeader.Get(rec."Document Type"::Invoice, rec."No.");
        PurchCalcDiscByType.ApplyInvDiscBasedOnAmt(InvoiceDiscountAmount, PurchaseHeader);
    end;

    local procedure ClearCalculatedFields()
    begin
        Clear(InvoiceDiscountAmount);
        Clear(DiscountAmountSet);
        TempFieldBuffer.DeleteAll();
    end;

    local procedure RegisterFieldSet(FieldNo: Integer)
    var
        LastOrderNo: Integer;
    begin
        LastOrderNo := 1;
        if TempFieldBuffer.FindLast() then
            LastOrderNo := TempFieldBuffer.Order + 1;

        Clear(TempFieldBuffer);
        TempFieldBuffer.Order := LastOrderNo;
        TempFieldBuffer."Table ID" := Database::"Purch. Inv. Entity Aggregate";
        TempFieldBuffer."Field ID" := FieldNo;
        TempFieldBuffer.Insert();
    end;

    local procedure CheckBuyFromVendor()
    begin
        if (rec."Buy-from Vendor No." = '') and
           (rec."Vendor Id" = BlankGUID)
        then
            Error(BuyFromVendorNotProvidedErr);
    end;

    local procedure CheckPermissions()
    var
        PurchaseHeader: Record "Purchase Header";
    begin
        PurchaseHeader.SetRange("Document Type", PurchaseHeader."Document Type"::Invoice);
        if not PurchaseHeader.ReadPermission() then
            Error(PurchaseInvoicePermissionsErr);

        HasWritePermission := PurchaseHeader.WritePermission();
    end;

    local procedure GetDraftInvoice(var PurchaseHeader: Record "Purchase Header")
    begin
        if rec.Posted then
            Error(DraftInvoiceActionErr);

        if not PurchaseHeader.GetBySystemId(rec.Id) then
            Error(CannotFindInvoiceErr);
    end;

    local procedure PostInvoice(var PurchaseHeader: Record "Purchase Header"; var PurchInvHeader: Record "Purch. Inv. Header")
    var
        LinesInstructionMgt: Codeunit "Lines Instruction Mgt.";
        PreAssignedNo: Code[20];
    begin
        LinesInstructionMgt.PurchaseCheckAllLinesHaveQuantityAssigned(PurchaseHeader);
        PreAssignedNo := PurchaseHeader."No.";
        PurchaseHeader.SendToPosting(Codeunit::"Purch.-Post");
        PurchInvHeader.SETCURRENTKEY("Pre-Assigned No.");
        PurchInvHeader.SetRange("Pre-Assigned No.", PreAssignedNo);
        PurchInvHeader.FindFirst();
    end;

    local procedure SetActionResponse(var ActionContext: WebServiceActionContext; InvoiceId: Guid)
    var
    begin
        ActionContext.SetObjectType(ObjectType::Page);
        ActionContext.SetObjectId(Page::"APIV2 - Purchase Invoices");
        ActionContext.AddEntityKey(rec.FieldNo(Id), InvoiceId);
        ActionContext.SetResultCode(WebServiceActionResultCode::Deleted);
    end;

    [ServiceEnabled]
    [Scope('Cloud')]
    procedure Post(var ActionContext: WebServiceActionContext)
    var
        PurchaseHeader: Record "Purchase Header";
        PurchInvHeader: Record "Purch. Inv. Header";
        PurchInvAggregator: Codeunit "Purch. Inv. Aggregator";
    begin
        GetDraftInvoice(PurchaseHeader);
        PostInvoice(PurchaseHeader, PurchInvHeader);
        SetActionResponse(ActionContext, PurchInvAggregator.GetPurchaseInvoiceHeaderId(PurchInvHeader));
    end;
}